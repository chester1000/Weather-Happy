apply plugin: 'android'

android {
    compileSdkVersion 19
    buildToolsVersion '19.1'

    defaultConfig {
        applicationId 'com.meeDamian.weather'
        minSdkVersion 15
        targetSdkVersion 19
        versionCode 1
        versionName '1.0'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    signingConfigs {
        release {
            storeFile     file(STORE_FILE)
            storePassword STORE_PASSWORD
            keyAlias      KEY_ALIAS
            keyPassword   KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                def file = variant.outputFile

                String parent = file.parent
                if( project.hasProperty('OUTPUT_DIR') && new File( (String)OUTPUT_DIR ).exists() )
                    parent = OUTPUT_DIR

                variant.outputFile = new File(
                    parent,
                    (String) file.name.replace(
                        ".apk",
                        "-" + defaultConfig.versionName + "-build" + defaultConfig.versionCode + ".apk"
                    )
                )
            }
        }
    }

    productFlavors {
        publisher {
            buildConfigField "String", "PARSE_KEY", PARSE_KEY_PUBLISHER
        }
        lurker {
            buildConfigField "String", "PARSE_KEY", PARSE_KEY_LURKER
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    publisherCompile project(":floatlabel")
}
